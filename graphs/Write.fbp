EXPORT=URL.IN:SERVER
EXPORT=ERROR.OUT:ERROR
EXPORT=INPUT.IN:IN
EXPORT=CONSTRUCT.PKEY:PKEY
EXPORT=PACKETIZE.OUT:OUT
EXPORT=WRITESERVER.QUIT:QUIT
EXPORT=PACKETIZE.FILTER:FILTER

'.+' -> REGEXP FilterToken(groups/FilterByGroup)
FilterToken() OUT -> IN Sanitize(pgorm/SanitizeObjects) OUT -> IN Construct(pgorm/ConstructWrite)
Url(core/Split) OUT -> SERVER WriteServer(pg/Postgres) ERROR -> IN Error(core/Merge)

FilterToken() GROUP -> TOKEN WriteServer()
Construct() TEMPLATE -> TEMPLATE WriteServer()
Construct() OUT -> IN WriteServer()

# Convert to NoFlo packets

WriteServer() OUT -> IN Packetize(pgorm/JsonToPackets)

# Only continue if config is ready. Otherwise buffer until it is.

'0' -> IN MergeReadiness(core/Merge) OUT -> ON Input(flow/TrafficLight) OUT -> IN FilterToken()

# Setup automatic filtering

Url() OUT -> SERVER ConfigServer(pg/Postgres) ERROR -> IN Error()

'columns' -> STRING ColumnsToken(strings/SendString)
'../config/read_columns.pgsql' -> IN Path(pgorm/ResolvePath) OUT -> IN ReadReadColumsFile(filesystem/ReadFileSync) OUT -> STRING ReadColumns(strings/SendString)
' ' -> STRING ColumnsActivator(strings/SendString)

Url() OUT -> IN ColumnsToken() OUT -> TOKEN ConfigServer()
Url() OUT -> IN ReadColumns() OUT -> TEMPLATE ConfigServer()
Url() OUT -> IN ColumnsActivator() OUT -> IN ConfigServer()

'columns' -> REGEXP FilterColumns(groups/FilterByGroup)
'table_name' -> GROUPING GroupColumnsByTable(objects/GroupValueByAnother)
'column_name' -> ENCLOSED GroupColumnsByTable()
ConfigServer() OUT -> IN FilterColumns() OUT -> IN GroupColumnsByTable() OUT -> IN Definitions(core/Split)

'1' -> STRING Ready(strings/SendString)

Definitions() OUT -> DEFINITION Sanitize()
Definitions() OUT -> IN Ready() OUT -> IN MergeReadiness()
Definitions() OUT -> READY Input()
